package module.protocols.domain;

enum module.protocols.domain.util.ProtocolResponsibleType as ProtocolResponsibleType;
enum module.protocols.domain.ProtocolVisibilityType as ProtocolVisibilityType;

valueType module.protocols.domain.util.ProtocolAction as ProtocolAction {
    externalizeWith {
        String toString();
    }

    internalizeWith fromString();
}

class ProtocolManager {
	Long currentSequenceNumber;
}

class Protocol {
    Boolean active;
    String observations;
    ProtocolAction protocolAction;
    String protocolNumber;
    Boolean renewable;
    String scientificAreas;
    LocalDate signedDate;
    
    ProtocolVisibilityType visibilityType;
}

class ProtocolHistory {
	DateTime timestamp;
	LocalDate beginDate;
	LocalDate endDate;
}

class ProtocolResponsible {
	ProtocolResponsibleType type;
	Strings positionList;
}

class ProtocolAuthorizationGroup {
}

class ProtocolAdministrativeGroup extends .pt.ist.bennu.core.domain.groups.legacy.PersistentGroup {
}

class ProtocolDirNode extends .module.fileManagement.domain.DirNode {
}

/* Relations */

relation ProtocolManagerMyOrg {
	.pt.ist.bennu.core.domain.MyOrg playsRole myOrg;
	ProtocolManager playsRole protocolManager;
}

relation ProtocolManagerProtocols {
	ProtocolManager playsRole protocolManager {
		multiplicity 1..1;
	}
	Protocol playsRole protocols {
		multiplicity *;
	}
}

relation ProtocolManagerInternalOrganizationModel {
	ProtocolManager playsRole internalProtocolManager;
	.module.organization.domain.OrganizationalModel playsRole internalOrganizationalModel;
}

relation ProtocolManagerExternalOrganizationModel {
	ProtocolManager playsRole externalProtocolManager;
	.module.organization.domain.OrganizationalModel playsRole externalOrganizationalModel;
}

relation ProtocolManagerAdministrativeGroup {
	ProtocolManager playsRole protocolManager {
		multiplicity 1..1;
	}
	ProtocolAdministrativeGroup playsRole administrativeGroup {
		multiplicity 1..1;
	}
}

relation ProtocolManagerCreatorsGroup {
	ProtocolManager playsRole protocolManager;
	.pt.ist.bennu.core.domain.groups.legacy.UnionGroup playsRole creatorsGroup;
}

relation ProtocolAdministrativeGroupPersistentGroup {
	ProtocolAdministrativeGroup playsRole administrativeGroup;
	.pt.ist.bennu.core.domain.groups.legacy.PersistentGroup playsRole delegateGroup;
}

// Protocols

relation ProtocolResponsibleProtocol {

	Protocol playsRole protocol;
	
	ProtocolResponsible playsRole protocolResponsible {
		multiplicity *;
	}
}

relation ProtocolResponsiblePerson {
	ProtocolResponsible playsRole protocolResponsible {
		multiplicity *;
	}
	.module.organization.domain.Person playsRole people {
		multiplicity *;
	}
}

relation ProtocolResponsibleUnit {
	ProtocolResponsible playsRole protocolResponsible {
		multiplicity *;
	}
	.module.organization.domain.Unit playsRole unit {
		multiplicity 1..1;
	}
}

relation ProtocolResponsibleCountry {
	ProtocolResponsible playsRole protocolResponsible {
		multiplicity *;
	}
	.module.geography.domain.Country playsRole country;
}

relation ProtocolHistoryProtocol {
	Protocol playsRole protocol {
		multiplicity 1..1;
	}
	ProtocolHistory playsRole protocolHistories {
		multiplicity *;
	}
}

// Access control

relation ProtocolAllowedToReadGroups {
	Protocol playsRole allowedToReadProtocol;
	.pt.ist.bennu.core.domain.groups.legacy.UnionGroup playsRole allowedToRead;
}

relation ProtocolAuthorizationGroupProtocol {
	Protocol playsRole writerProtocols {
		multiplicity *;
	}
	ProtocolAuthorizationGroup playsRole writerGroup;
}

relation ProtocolReaders {
	Protocol playsRole readerProtocols {
		multiplicity *;
	}
	.pt.ist.bennu.core.domain.groups.legacy.PersistentGroup playsRole readerGroups {
		multiplicity *;
	}
}

relation ProtocolAuthorizationGroupProtocolManager {
	ProtocolManager playsRole protocolManager {
		multiplicity 1..1;
	}
	ProtocolAuthorizationGroup playsRole protocolAuthorizationGroups {
		multiplicity *;
	}
}

relation ProtocolAuthorizationGroupWriterGroups {
	ProtocolAuthorizationGroup playsRole protocolAuthorizationGroupAllowedToWrite;
	.pt.ist.bennu.core.domain.groups.legacy.PersistentGroup playsRole authorizedWriterGroup;
}

relation ProtocolAuthorizationGroupReaderGroups {
	ProtocolAuthorizationGroup playsRole protocolAuthorizationGroupAllowedToRead {
		multiplicity *;
	}
	.pt.ist.bennu.core.domain.groups.legacy.PersistentGroup playsRole authorizedReaderGroups {
		multiplicity *;
	}
}

relation ProtocolAuthorizationGroupDirNode {
	ProtocolAuthorizationGroup playsRole ownerGroup;
	ProtocolDirNode playsRole groupDir;
}

relation ProtocolFilesNode {
	Protocol playsRole protocol;
	ProtocolDirNode playsRole protocolDir;
}
